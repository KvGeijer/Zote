read("aoc-2022/inputs/10.txt")
	>> split("\n")
	>> map(\>> split(" "))
	:>> instrs;

cycle_pos := [];
current := 1;
for instr in instrs {
	match instr {
		("addx", val) -> {
			current >> push(cycle_pos) >> push(cycle_pos);
			current += int(val);
		},
		_ -> current >> push(cycle_pos), // TODO: Pattern match against singleton iter
	}
}

// Part 1
(cycle_pos >> enumerate)[19::40] 
	>> map(\(cycle, val) -> val * (cycle + 1)) 
	>> sum 
	>> print;

// Part 2
crt := [];
(rows, cols) := [6, 40];
for row in [0:rows] {
	line := [];
	for col in [0:cols] {
		pos := cycle_pos[row*cols + col];
		(if abs(pos - col) <= 1 '#' else ' ') >> push(line);
	}
	line >> join("") >> push(crt);
}

crt >> join("\n") >> print;



